# -- constants --
  ns = 1e-9 ; ps = 1e-12 ; fs = 1e-15 ;
  FWHM2Gauss = (1/2.35482) ;

# -- position particles using particle sets --		
  if (!defined(Qtotal)) Qtotal = N*qe;
  setparticles("beam",N,me,qe,Qtotal) ;			# Declaration and definition of particles
  setrxydist("beam","~g",0,sigma,sleft,sright) ;	# Gaussian radial distribution
# setrxydist("beam","~u",Rbunch/2,Rbunch) ;		# Uniform radial distribution
# setrxydist("beam","~s",0,Rbunch);			# Half-circle distribution
  setphidist("beam","~u",0,2*pi) ;			# Uniform distribution in phi
  settdist( "beam", "~g", 0,sigt*FWHM2Gauss,3,3) ;	# Gaussian distribution in time
  zminmax("wcs","I", -1e-6,100) ;			# Prevents particle in -z direction

# -- initial thermal emittance --
  setGBzdist("beam", "~u", Tbunch,0 ) ;			# Emittance=0.46 um for uniform Rb=1 mm
  setGBthetadist("beam", "~u", pi/4,pi/2 ) ;
  setGBphidist("beam", "~u", 0,2*pi ) ;

# -- external electrostatic field from Superfish --
  map2D_E("wcs","z",0,"maps/map100kVgun10um.gdf","R","Z","Er","Ez",Efact) ;
  forwardscatter("wcs","I","remove", 0) ;		# Particles are removed by anode
  scatteriris("wcs","z",zIris,Rhole,1) scatter="remove" ;

# --rectcoil--
  rectcoil("wcs","z", zsol,R1sol, R2sol, L, Isol) ;
  rectcoil("wcs","z",-zsol,R1sol, R2sol, L,-Isol) ;

# --spacecharge--
  #spacecharge3Dmesh("BeamScale",1e-3,1e+3,"Cathode","Boundaries","AAAAAA","MeshBoxAccuracy",0.02);


# -- sample --
  if (defined(scale)) {
    if (!defined(zsample)) zsample = 0.588;
    EELSbugged("wcs","I","test","testSpectrum.dat",scale);  
    scatterplate("wcs","z",zsample,1,1) scatter = "test";
  }
# --single loop solenoid--
  if(!defined(Isol2)) Isol2=689;
  solenoid("wcs","z", zsol2,Rsol2,Isol2) ;

#--cavity----
  lcav = 5e-3;
  if (!defined(zcav)) zcav = 0.6;
  if (!defined(E0)) E0 = 1.59e6;
  sigmaE = 1.95e-3;
  omega = 2*pi*3e9;
  if (!defined(phi)) phi = -0.795;
  TM010erf("wcs","z",zcav, E0,lcav,sigmaE,phi,omega);

# -- streak --
  if (streak!=0){
  	if (defined(B0)) {
   	 if (!defined(z110)) z110 = 1.6;
   	 L110 = 17e-3;
  	 sigmaB110 = 0.75e-3;
   	 xE110 = 8.24e-3;
  	 E110 = 2.72e6*B0;
    	sigmaE110 = 0.91e-3;
    	omega110 = 2*pi*3e9;
    	if (!defined(phi110)) phi110 = 1.885;
    	TM110erf("wcs","z",z110, B0,L110,sigmaB110,phi110,omega110);
    	TE110gauss("wcs","z",z110-xE110, E110,sigmaE110,phi110,omega110);
    	TE110gauss("wcs","z",z110+xE110, -E110,sigmaE110,phi110,omega110);
  	}
  }
  if(defined(Isol3)) solenoid("wcs","z", 1.8,Rsol2,Isol3) ;

# -- output --
  accuracy(5) ;
  screen( "wcs","I", 1.6 ) ;
  if (!defined(outputT)) outputT = 1;
  if (outputT!=0) {
    tout(3e-9,13e-9,100e-12) ;
  }
  if (!defined(outputZ)) outputZ = 0;
  if (outputZ!=0) screen("wcs","I",outputZ);

  dtmax=0.2e-9;
